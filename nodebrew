#!/usr/bin/env perl

# nodebrew
# Node.js version manager
#
# @author  Kazuhito Hokamura <k.hokamura@gmail.com>
# @url     https://github.com/hokaccha/nodebrew
# @version 0.1.0

use strict;
use warnings;
use File::Path qw/rmtree/;
use File::Copy qw/copy/;

our $VERSION = '0.1.0';
my $cmd = $ARGV[0] || 'help';
my $version = $ARGV[1];
my @cmds = qw/use install uninstall list ls setup/;
my $brew_dir = $ENV{NODEBREW_HOME} || $ENV{HOME} . '/.nodebrew';
my $src_dir = $brew_dir . '/src';
my $node_dir = $brew_dir . '/node';
my $current = $brew_dir . '/current';
my $default_dir = $brew_dir . '/default';

# check curl or wget
my $hascurl = `which curl`;
my $haswget = `which wget`;
if (!$hascurl && !$haswget) {
    die 'Need curl or wget';
}

sub cmd_use {
    my $target = "$node_dir/$version";
    my $nodebrew = "$target/bin/nodebrew";
    return print "$version is not installed\n" unless -e $target;
    unlink $current if -l $current;
    symlink $target, $current;
    symlink "$brew_dir/nodebrew", $nodebrew unless -l $nodebrew;
    print "use $version\n";
}

sub cmd_install {
    return print "$version is already installed\n" if -e "$node_dir/$version";

    my @tarballs = (
        "http://nodejs.org/dist/$version/node-$version.tar.gz",
        "http://nodejs.org/dist/node-$version.tar.gz",
    );
    my $tarball = '';
    for (@tarballs) {
        if (check_url($_)) {
            $tarball = $_;
            last;
        }
    }
    return print "$version is not found\n" unless $tarball;

    unlink "$src_dir/node-$version.tar.gz";
    rmtree "$src_dir/node-$version";
    print "fetch: $tarball\n";

    my $fetch_cmd = $hascurl
        ? "curl -C - --progress-bar $tarball -o"
        : "wget -c $tarball -O";
    my $install_cmd = qq[
        cd "$src_dir" &&
        $fetch_cmd "node-$version.tar.gz" &&
        tar -xzf "node-$version.tar.gz" &&
        cd "node-$version" &&
        ./configure --prefix="$node_dir/$version" &&
        make &&
        make install
    ];

    # for debug
    # print $install_cmd;

    system $install_cmd;
}

sub cmd_uninstall {
    my $target = "$node_dir/$version";
    my $current_version = get_current_version();
    return print "$version is not installed\n" unless -e $target;
    rmtree $target;
    if ($current_version eq $version) {
        use_default();
    }
    print "$version uninstalled\n";
}

sub cmd_list {
    opendir my $dh, $node_dir or die $!;
    while (my $dir = readdir $dh) {
        print "$dir\n" unless $dir =~ '^\.\.?$';
    }

    print "\ncurrent: " . get_current_version() . "\n";
}

sub cmd_ls {
    cmd_list();
}

sub cmd_setup {
    mkdir $brew_dir;
    mkdir $src_dir;
    mkdir $node_dir;
    mkdir $default_dir;
    mkdir "$default_dir/bin";

    my $nodebrew_path = "$brew_dir/nodebrew";
    my $nodebrew_source = fetch_nodebrew();
    die "Error: fetch nodebrew faild.\n" unless $nodebrew_source;

    open my $fh, '>', $nodebrew_path or die "Error: $!";
    print $fh $nodebrew_source;
    `chmod +x $nodebrew_path`;

    symlink $nodebrew_path, "$default_dir/bin/nodebrew";
    use_default() if get_current_version() eq 'none';

    print "install nodebrew in $brew_dir\n\n";
    print "Add your \$PATH: $node_dir/current/bin\n";
}

sub cmd_help {
    print <<"...";
nodebrew $VERSION

Usage:
    nodebrew help                    Show this message
    nodebrew install <version>       Download and install a <version>
    nodebrew uninstall <version>     Uninstall a version
    nodebrew use <version>           Modify PATH to use <version>
    nodebrew list                    List installed versions
    nodebrew ls                      Alias list
    nodebrew setup                   Install nodebrew

Example:
    nodebrew install v0.6.0     Install a specific version number
    nodebrew use v0.6.0         Use a specific version number
...
}

sub use_default {
    unlink $current if -l $current;
    symlink $default_dir, $current;
}

sub get_current_version {
    return 'none' unless -l $current;
    my $current_version = readlink $current;
    $current_version =~ m!^$node_dir/(.+)!;
    return $1 || 'none';
}

sub fetch_nodebrew {
    my $fetch_url = 'https://raw.github.com/hokaccha/nodebrew/master/nodebrew';
    my $fetch_cmd = $hascurl ? "curl -s $fetch_url" : "wget -q $fetch_url -O -";
    return unless check_url($fetch_url);
    print "fetching nodebrew...\n";
    return `$fetch_cmd`;
}

sub check_url {
    my $url = shift;
    my $check_cmd = $hascurl ? 'curl -Is' : 'wget -Sq --spider';
    return `$check_cmd "$url" 2>&1 | grep '200 OK'`;
}

if (0 == scalar grep { $_ eq $cmd } @cmds) {
    cmd_help();
}
else {
    no strict 'refs';
    *{'cmd_' . $cmd}->();
}

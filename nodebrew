#!/usr/bin/env perl

# nodebrew
# Node.js version manager
#
# @author  Kazuhito Hokamura <k.hokamura@gmail.com>
# @url     https://github.com/hokaccha/nodebrew
# @version 0.2.0

use strict;
use warnings;

package Nodebrew::Fetcher;

sub new {
    my ($class, $type) = @_;

    $type eq 'wget' ? Nodebrew::Fetcher::wget->new:
    $type eq 'curl' ? Nodebrew::Fetcher::curl->new:
    die 'Fetcher type invalid';
}

package Nodebrew::Fetcher::curl;

sub new {
    my $class = shift;
    my $self = {};

    bless $self, $class;
    return $self;
}

sub fetch_able {
    my ($self, $url) = @_;

    `curl -Is "$url"` =~ m/200 OK/;
}

sub fetch {
    my ($self, $url) = @_;

    `curl -s $url`;
}

sub download {
    my ($self, $url, $path) = @_;

    system "curl -C - --progress-bar $url -o $path";
}

package Nodebrew::Fetcher::wget;

sub new {
    my $class = shift;
    my $self = {};

    bless $self, $class;
    return $self;
}

sub fetch_able {
    my ($self, $url) = @_;

    `wget -Sq --spider "$url" 2>&1` =~ m/200 OK/;
}

sub fetch {
    my ($self, $url) = @_;

    `wget -q $url -O -`;
}

sub download {
    my ($self, $url, $path) = @_;

    system "wget -c $url -O $path";
}


package Nodebrew;

use File::Path qw/rmtree/;

our $VERSION = '0.2.0';
my $cmd = $ARGV[0] || 'help';
my $version = $ARGV[1];
my @cmds = qw/use install uninstall list ls setup selfupdate/;
my $brew_dir = $ENV{'NODEBREW_ROOT'} || $ENV{'HOME'} . '/.nodebrew';
my $src_dir = $brew_dir . '/src';
my $node_dir = $brew_dir . '/node';
my $current = $brew_dir . '/current';
my $default_dir = $brew_dir . '/default';
my $nodebrew_url = $ENV{'NODEBREW_URL'}
    || 'https://raw.github.com/hokaccha/nodebrew/master/nodebrew';

# check curl or wget
my $hascurl = `which curl`;
my $haswget = `which wget`;
if (!$hascurl && !$haswget) {
    die 'Need curl or wget';
}

sub cmd_use {
    my $target = "$node_dir/$version";
    my $nodebrew = "$target/bin/nodebrew";
    return print "$version is not installed\n" unless -e $target;
    unlink $current if -l $current;
    symlink $target, $current;
    symlink "$brew_dir/nodebrew", $nodebrew unless -l $nodebrew;
    print "use $version\n";
}

sub cmd_install {
    return print "$version is already installed\n" if -e "$node_dir/$version";

    my @tarballs = (
        "http://nodejs.org/dist/$version/node-$version.tar.gz",
        "http://nodejs.org/dist/node-$version.tar.gz",
    );
    my $tarball = '';
    for (@tarballs) {
        if (check_url($_)) {
            $tarball = $_;
            last;
        }
    }
    return print "$version is not found\n" unless $tarball;

    unlink "$src_dir/node-$version.tar.gz";
    rmtree "$src_dir/node-$version";
    print "fetch: $tarball\n";

    my $fetch_cmd = $hascurl
        ? "curl -C - --progress-bar $tarball -o"
        : "wget -c $tarball -O";
    my $install_cmd = qq[
        cd "$src_dir" &&
        $fetch_cmd "node-$version.tar.gz" &&
        tar -xzf "node-$version.tar.gz" &&
        cd "node-$version" &&
        ./configure --prefix="$node_dir/$version" &&
        make &&
        make install
    ];

    system $install_cmd;
}

sub cmd_uninstall {
    my $target = "$node_dir/$version";
    my $current_version = get_current_version();
    return print "$version is not installed\n" unless -e $target;
    rmtree $target;
    if ($current_version eq $version) {
        use_default();
    }
    print "$version uninstalled\n";
}

sub cmd_list {
    opendir my $dh, $node_dir or die $!;
    while (my $dir = readdir $dh) {
        print "$dir\n" unless $dir =~ '^\.\.?$';
    }

    print "\ncurrent: " . get_current_version() . "\n";
}

sub cmd_ls {
    cmd_list();
}

sub cmd_setup {
    mkdir $brew_dir;
    mkdir $src_dir;
    mkdir $node_dir;
    mkdir $default_dir;
    mkdir "$default_dir/bin";

    my $nodebrew_path = "$brew_dir/nodebrew";
    fetch_nodebrew();
    `chmod +x $nodebrew_path`;
    symlink $nodebrew_path, "$default_dir/bin/nodebrew";
    use_default() if get_current_version() eq 'none';

    $brew_dir =~ s/$ENV{'HOME'}/\$HOME/;
    print "install nodebrew in $brew_dir\n\n";
    print "========================================\n";
    print "Add path:\n\n";
    print "export PATH=$brew_dir/current/bin:\$PATH\n";
    print "========================================\n";
}

sub cmd_selfupdate {
    fetch_nodebrew();
    print "update successfull\n";
}

sub cmd_help {
    print <<"...";
nodebrew $VERSION

Usage:
    nodebrew help                    Show this message
    nodebrew install <version>       Download and install a <version>
    nodebrew uninstall <version>     Uninstall a version
    nodebrew use <version>           Modify PATH to use <version>
    nodebrew list                    List installed versions
    nodebrew ls                      Alias list
    nodebrew selfupdate              Update nodebrew

Example:
    nodebrew install v0.6.0     Install a specific version number
    nodebrew use v0.6.0         Use a specific version number
...
}

sub use_default {
    unlink $current if -l $current;
    symlink $default_dir, $current;
}

sub get_current_version {
    return 'none' unless -l $current;
    my $current_version = readlink $current;
    $current_version =~ m!^$node_dir/(.+)!;
    return $1 || 'none';
}

sub fetch_nodebrew {
    my $fetch_cmd = $hascurl
        ? "curl -s $nodebrew_url"
        : "wget -q $nodebrew_url -O -";
    my $nodebrew_path = "$brew_dir/nodebrew";
    die "Error: fetch nodebrew faild.\n" unless check_url($nodebrew_url);

    print "fetching nodebrew...\n";
    my $nodebrew_source = `$fetch_cmd`;

    open my $fh, '>', $nodebrew_path or die "Error: $!";
    print $fh $nodebrew_source;
}

sub check_url {
    my $url = shift;
    return 1 if $url =~ /^file/; # for debug
    my $check_cmd = $hascurl ? 'curl -Is' : 'wget -Sq --spider';
    return `$check_cmd "$url" 2>&1 | grep '200 OK'`;
}

unless (caller) {
    if (0 == scalar grep { $_ eq $cmd } @cmds) {
        cmd_help();
    }
    else {
        no strict 'refs';
        *{'cmd_' . $cmd}->();
    }
}
